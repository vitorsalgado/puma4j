plugins {
  id "idea"
  id "org.sonarqube" version "4.4.1.3373"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
  id "org.jetbrains.kotlin.jvm" version "1.8.10" apply false
  id "org.owasp.dependencycheck" version "8.1.0"
}

def Version = file("$rootDir/VERSION.txt").text.trim()

apply from: "dependencies.gradle"
apply from: "setup.gradle"

ext {
  isCI = System.getenv("CI") == "true"

  coreProjects = subprojects.findAll {
    p -> !p.name.contains("example") && !p.name.endsWith("-bom")
  }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
    }
  }
}

allprojects {
  apply plugin: "jacoco"

  group "io.github.vitorsalgado.puma4j"
  version Version
  description "Easy and convenient way to load file resources into your tests using only annotations."

  repositories {
    mavenCentral()
  }

  jacoco {
    toolVersion = "0.8.7"
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "java-library"
  apply plugin: 'kotlin'
  apply plugin: "checkstyle"
  apply plugin: "pmd"
  apply from: "${rootDir}/lint.gradle"

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  tasks.withType(JavaCompile) { compileTask ->
    sourceCompatibility = "8"
    targetCompatibility = "8"
    options.encoding = 'UTF-8'
    options.deprecation = true
    options.compilerArgs += ["-Xlint:-deprecation", "-Xlint:unchecked", "-parameters"]
  }

  test {
    useJUnitPlatform()

    testLogging {
      events "passed", "skipped", "failed"
      showStackTraces true
      exceptionFormat "full"
    }

    finalizedBy jacocoTestReport
  }

  // Common dependencies shared by all modules
  // --
  dependencies {
    testImplementation libs.junitJupiterApi
    testRuntimeOnly libs.junitJupiterEngine
  }

  jacocoTestReport {
    dependsOn test
  }
}

configure(coreProjects) {
  apply plugin: "maven-publish"
  apply plugin: "signing"
  apply from: "${rootDir}/publishing.gradle"

  afterEvaluate {
    jar {
      inputs.property('moduleName', moduleName)
      manifest.attributes(
          'Automatic-Module-Name': moduleName
      )
    }
  }
}

def allTestCoverageFile = "${rootProject.projectDir}/build/reports/jacoco/report.xml"

task jacocoMergeTest(type: JacocoMerge) {
  destinationFile = file(allTestCoverageFile)
  executionData = project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
}

task jacocoRootTestReport(type: JacocoReport) {
  coreProjects.each { dependsOn("${it.name}:test") }
  coreProjects.each { dependsOn("${it.name}:jacocoTestReport") }

  executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  onlyIf {
    true
  }

  reports {
    xml.required.set true
    xml.destination file(allTestCoverageFile)
    html.required.set true
    csv.required.set false
  }
}

sonarqube {
  properties {
    property "sonar.language", "java"
    property "sonar.projectKey", "vitorsalgado_puma4j"
    property "sonar.projectName", "puma4j"
    property "sonar.organization", "vitorsalgado"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.links.scm", "https://github.com/vitorsalgado/puma4j"
    property "sonar.links.issue", "https://github.com/vitorsalgado/puma4j/issues"
    property "sonar.links.homepage", "https://github.com/vitorsalgado/puma4j"
    property "sonar.apex.pmd.reportPath", "${rootProject.projectDir}/build/reports/pmd/main.xml"
    property "sonar.coverage.jacoco.xmlReportPaths", "${allTestCoverageFile}"
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
